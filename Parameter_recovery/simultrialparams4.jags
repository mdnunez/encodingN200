
model {
    ##########
    #Fixed Parameters
    beta <- .5
    ##########
    #Between-EEGsession variability in motor response time
    rmrsubsd ~ dgamma(.2,1)

    #Between-EEGsession variability in drift rate
    deltasubsd ~ dgamma(1,1)

    #Between-EEGsession variability in boundary separation
    alphasubsd ~ dgamma(1,1)

    #Between-EEGsession variability in VET variability
    vetsdsubsd ~ dgamma(.2,1)

    ##########
    #Block-level parameters
    ##########
    #Condition-level VET variability
    vetsdcond ~ dnorm(.05, pow(.025,-2))

    #Condition-level motor response time
    rmrcond ~ dnorm(.2, pow(.1,-2))

    #Condition-level drift rate
    deltacond ~ dnorm(1, pow(2, -2))

    #Condition-level boundary separation
    alphacond ~ dnorm(1, pow(.5,-2))

   # Subject-level parameters
    for (sub in 1:nsubs) {
        # Subject-level motor response time (RMR)
        rmrsub[sub] ~ dnorm(rmrcond, pow(rmrsubsd, -2))T(0,.5)

        # Subject-level drift rate
        deltasub[sub] ~ dnorm(deltacond, pow(deltasubsd, -2))T(-9, 9)

        # Subject-level boundary separation
        alphasub[sub] ~ dnorm(alphacond, pow(alphasubsd, -2))T(.1,2)

        # Subject-level VET variability
        vetsdsub[sub] ~ dnorm(vetsdcond, pow(vetsdsubsd, -2))T(0, 1)

        # Subject-level mind wandering
        probsub[sub,1:2] ~ ddirch(c(1,1))
    }

    ##########
    # Wiener likelihoods
    for (i in 1:N) {

        dt[i] ~ dwiener(alphasub[subject[i]],
        0.001,
        beta,
        deltasub[subject[i]])

        vetsub[i] <- abs(y[i]) -  abs(dt[i]) - rmrsub[subject[i]]

        #Note that N200 latencies are not censored
        n200lat[i] ~ dnorm(vetsub[i],
            pow(vetsdsub[subject[i]],-2))

        # Log density for DDM process
        ld_comp[i, 1] <- dlogwiener(y[i] - ifelse((y[i] > 0),1,-1)*n200lat[i],alphasub[subject[i]],
        rmrsub[subject[i]],
        beta,
        deltasub[subject[i]])

        # Log density for lapse trials (negative max RT to positive max RT)
        ld_comp[i, 2] <- logdensity.unif(y[i], -maxrt[subject[i]], maxrt[subject[i]])

        # Select one of these two densities (Mixture of nonlapse and lapse trials)
        density[i] <- exp(ld_comp[i, component_chosen[i]] - Constant)
        
        # Generate a likelihood for the MCMC sampler using a trick to maximize density value
        Ones[i] ~ dbern(density[i])

        # Probability of mind wandering trials (lapse trials)
        component_chosen[i] ~ dcat(probsub[subject[i],1:2])
    }
}
