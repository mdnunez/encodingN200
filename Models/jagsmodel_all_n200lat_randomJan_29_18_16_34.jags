
model {
	##########
	#Fixed Parameters
	beta <- .5
	##########
	#Between-subject variability in residual non-decision time
	tersubsd ~ dgamma(.2,1)

	#Between-subject variability in drift
	deltasubsd ~ dgamma(1,1)

	#Between-subject variability in log() boundary separation
	alphasubsd ~ dgamma(1,1)

	#Between-subject variability in N200 latency
	n200subsd ~ dgamma(.2,1)

	#Between-trial variability in N200 latency
	n200trialsd ~ dgamma(.2,1)

	for (f in 1:3) {

		#Hierarchical-level effect of N200 latency
		n1gammault[1,f] ~ dnorm(1,pow(3,-2)) #'Informative' prior

		#Between-condition variability in effect of N200 latency
		n1gammasd[1,f] ~ dgamma(1,1)

		#Between-subject variability in N200 effect
		n1gammasubsd[1,f] ~ dgamma(1,1)
	}

	##########
	#Block-level parameters
	##########
	for (k in 1:nconds) {
		for (e in 1:nexps) {
			#Condition-level N200 latency
			n200cond[e,k] ~ dnorm(.2, pow(.1,-2))

	        #Condition-level residual non-decision time
			tercond[e,k] ~ dnorm(.3, pow(.25,-2))

			#Condition-level residual drift rate
			deltacond[e,k] ~ dnorm(1, pow(2, -2))

			#Condition-level residual log() boundary separation
			alphacond[e,k] ~ dnorm(0, pow(.7,-2))

			#Condition-level effects of N200 latency on non-decision time, drift rate, and boundary separation
			for (f in 1:3) {
				n1gammacond[f,e,k] ~ dnorm(n1gammault[1,f],pow(n1gammasd[1,f],-2))
			}
		}
		
		#Subject-level parameters
		for (ses in 1:nses) {
			#Subject-level residual non-decision time
			tersub[k,ses] ~ dnorm(tercond[experiment[ses]+1,k], 
				pow(tersubsd, -2))

			#Subject-level residual drift rate
			deltasub[k,ses] ~ dnorm(deltacond[experiment[ses]+1,k], 
				pow(deltasubsd, -2))

			#Subject-level diffusion coefficient
			alphasub[k,ses] ~ dnorm(alphacond[experiment[ses]+1,k], 
				pow(alphasubsd, -2))

		    #Subject-level N200 latency
			n200sub[k,ses] ~ dnorm(n200cond[experiment[ses]+1,k], 
				pow(n200subsd, -2))

			#Subject-level effects of N200 latency on non-decision time, drift rate, and boundary separation
			for (f in 1:3) {
				n1gammasub[f,k,ses] ~ dnorm(n1gammacond[f,experiment[ses]+1,k],
					pow(n1gammasubsd[1,f],-2))
			}
		}
	}
	##########
	#Wiener likelihoods
	for (i in 1:N) {
		#Note that N200 latencies are censored between 150 and 274 ms
		n200lat[i] ~ dnorm(n200sub[condition[i], EEGsession[i]],
			pow(n200trialsd,-2))T(.151,.274)

		y[i] ~ dwiener(exp(alphasub[condition[i],EEGsession[i]]
		+ n1gammasub[3, condition[i], EEGsession[i]]*n200lat[i]),
		tersub[condition[i],EEGsession[i]]
		+ n1gammasub[1, condition[i],EEGsession[i]]*n200lat[i], 
		beta, 
		deltasub[condition[i],EEGsession[i]]
		+ n1gammasub[2, condition[i],EEGsession[i]]*n200lat[i])
	}
}
