
model {
	##########
	#Fixed Parameters
	beta <- .5
	##########
	#Between-EEGsession variability in motor response time
	rmrsubsd ~ dgamma(.2,1)

	#Between-EEGsession variability in visual encoding time
	vetsubsd ~ dgamma(.2,1)

	#Between-EEGsession variability in drift rate
	deltasubsd ~ dgamma(1,1)

	#Between-EEGsession variability in boundary separation
	alphasubsd ~ dgamma(1,1)

	#Between-trial variability in single-trial N200 latencies
	n200trialsd ~ dgamma(.2,1)

	##########
	#Block-level parameters
	##########
	for (k in 1:nconds) {
		for (e in 1:nexps) {
			#Condition-level visual encoding time
			vetcond[e,k] ~ dnorm(.2, pow(.1,-2))

	        #Condition-level motor response time
			rmrcond[e,k] ~ dnorm(.2, pow(.1,-2))

			#Condition-level drift rate
			deltacond[e,k] ~ dnorm(1, pow(2, -2))

			#Condition-level boundary separation
			alphacond[e,k] ~ dnorm(1, pow(.5,-2))

		}
		#EEGsession-level parameters
		for (ses in 1:nses) {
			#EEGsession-level motor response time (RMR)
			rmrsub[k,ses] ~ dnorm(rmrcond[experiment[ses]+1,k],
				pow(rmrsubsd, -2))T(0,.5)

			#EEGsession-level drift rate
			deltasub[k,ses] ~ dnorm(deltacond[experiment[ses]+1,k],
				pow(deltasubsd, -2))T(-9, 9)

			#EEGsession-level boundary separation
			alphasub[k,ses] ~ dnorm(alphacond[experiment[ses]+1,k],
				pow(alphasubsd, -2))T(.1,2)

		    #EEGsession-level visual encoding time (VET)
			vetsub[k,ses] ~ dnorm(vetcond[experiment[ses]+1,k],
				pow(vetsubsd, -2))

			#EEGsession-level lapse trials
        	probsub[k, ses, 1:2] ~ ddirch(c(1,1))
		}
	}
	##########
	# Wiener likelihoods
	for (i in 1:N) {
		#Trial-level non-decision time (NDT)
        tertrial[i] ~ sum(n200lat[i], rmrsub[condition[i],EEGsession[i]])

		#Note that N200 latencies are censored between 150 and 274 ms
		n200lat[i] ~ dnorm(vetsub[condition[i], EEGsession[i]],
			pow(n200trialsd,-2))T(.151,.274)

        # Log density for DDM process
        ld_comp[i, 1] <- dlogwiener(y[i],alphasub[condition[i],EEGsession[i]],
        tertrial[i],
        beta,
        deltasub[condition[i],EEGsession[i]])

        # Log density for lapse trials (negative max RT to positive max RT)
        ld_comp[i, 2] <- logdensity.unif(y[i], -maxrt[condition[i],EEGsession[i]], maxrt[condition[i],EEGsession[i]])

        # Select one of these two densities (Mixture of nonlapse and lapse trials)
        density[i] <- exp(ld_comp[i, component_chosen[i]] - Constant)
		
        # Generate a likelihood for the MCMC sampler using a trick to maximize density value
        Ones[i] ~ dbern(density[i])

        # Probability of mind wandering trials (lapse trials)
        component_chosen[i] ~ dcat(probsub[condition[i],EEGsession[i],1:2])
	}
}
