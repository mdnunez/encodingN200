
model {
	##########
	#Fixed Parameters
	beta <- .5
	##########
	#Between-session variability in non-decision time
	tersubsd ~ dgamma(.2,1)

	#Between-session variability in drift
	deltasubsd ~ dgamma(1,1)

	#Between-session variability in boundary separation
	alphasubsd ~ dgamma(1,1)

	#Between-session variability in N1 latency
	n1subsd ~ dgamma(.2,1)

	##########
	#Block-level parameters
	##########

	#Condition-level effects of N1 latency on non-decision time
	for (f in 1:3) {
		n1gammacond[f,1,3] ~ dnorm(1,pow(3,-2))
		n1gammacond[f,2,3] ~ dnorm(0,pow(1,-2))
		for (e in 1:nexps) {
			for (k in 1:2) {
				n1gammacond[f,e,k] ~ dnorm(0,pow(1,-2))
			}
		}
	}

	for (k in 1:nconds) {
		for (e in 1:nexps) {
			#Condition-level N1 latency
			n1cond[e,k] ~ dnorm(.2, pow(.1,-2))

	        #Condition-level non-decision time
			tercond[e,k] ~ dnorm(.3, pow(.25,-2))

			#Condition-level drift rate
			deltacond[e,k] ~ dnorm(1, pow(2, -2))

			#Condition-level boundary separation
			alphacond[e,k] ~ dnorm(1, pow(.5,-2))

		}
		#EEGsession-level parameters
		for (ses in 1:nses) {
			#EEGsession-level non-decision time
			tersub[k,ses] ~ dnorm(tercond[experiment[ses]+1,k]
				+ n1gammacond[1,1,3]*n1sub[k,ses]
				+ n1gammacond[1,1,2]*(k<3)*n1sub[k,ses]
				+ n1gammacond[1,1,1]*(k<2)*n1sub[k,ses]
				+ n1gammacond[1,2,3]*experiment[ses]*n1sub[k,ses]
				+ n1gammacond[1,2,2]*(k<3)*experiment[ses]*n1sub[k,ses]
				+ n1gammacond[1,2,1]*(k<2)*experiment[ses]*n1sub[k,ses],
				pow(tersubsd, -2))T(0,1)

			#EEGsession-level drift rate
			deltasub[k,ses] ~ dnorm(deltacond[experiment[ses]+1,k]
				+ n1gammacond[2,1,3]*n1sub[k,ses]
				+ n1gammacond[2,1,2]*(k<3)*n1sub[k,ses]
				+ n1gammacond[2,1,1]*(k<2)*n1sub[k,ses]
				+ n1gammacond[2,2,3]*experiment[ses]*n1sub[k,ses]
				+ n1gammacond[2,2,2]*(k<3)*experiment[ses]*n1sub[k,ses]
				+ n1gammacond[2,2,1]*(k<2)*experiment[ses]*n1sub[k,ses],
				pow(deltasubsd, -2))T(-9, 9)

			#EEGsession-level boundary separation
			alphasub[k,ses] ~ dnorm(alphacond[experiment[ses]+1,k]
				+ n1gammacond[3,1,3]*n1sub[k,ses]
				+ n1gammacond[3,1,2]*(k<3)*n1sub[k,ses]
				+ n1gammacond[3,1,1]*(k<2)*n1sub[k,ses]
				+ n1gammacond[3,2,1]*experiment[ses]*n1sub[k,ses]
				+ n1gammacond[3,2,2]*(k<3)*experiment[ses]*n1sub[k,ses]
				+ n1gammacond[3,2,3]*(k<2)*experiment[ses]*n1sub[k,ses],
				pow(alphasubsd, -2))T(.1,3)

		    #EEGsession-level N1 latency
			n1sub[k,ses] ~ dnorm(n1cond[experiment[ses]+1,k],pow(n1subsd, -2))
		}
	}
	##########
	#Wiener likelihoods
	for (i in 1:N) {
		y[i] ~ dwiener(alphasub[condition[i],EEGsession[i]],
		tersub[condition[i],EEGsession[i]],
		beta,
		deltasub[condition[i],EEGsession[i]])
	}
}
